apply plugin: "application"
apply plugin: 'jacoco'
apply plugin: "antlr"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

mainClassName = "com.kasirgalabs.etumulator.ETUmulator"

repositories {
    mavenCentral()
}

dependencies {
    antlr group: "org.antlr", name: "antlr4", version: "4.6"
    testCompile group: "junit", name: "junit", version: "4.12"
}

generateGrammarSource {
    arguments += ["-package", "com.kasirgalabs.arm", "-visitor", "-long-messages"]
    doLast {
        copy {
            from "build/generated-src/antlr/main/"
            include "*.java"
            into "src/main/java/com/kasirgalabs/arm"
        }
        project.delete fileTree("build/generated-src/antlr/main").include("*.*")
    }
}

jar {
    manifest {
        attributes "Main-Class": mainClassName
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task wrapper(type: Wrapper) {
   gradleVersion = "2.10"
}
